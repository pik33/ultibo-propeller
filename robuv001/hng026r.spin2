'********************************************************************************************
' Nostalgic HDMI for P2 - 1 cog version - for ROBUV
' Version 0.26 alpha -20210711
' 1 cog 800x480 graphics, redefinable font
' (c) 2012-2021 Piotr Kardasz pik33@o2.pl
' MIT license
'
'***************************************************************************************************

CON


hdmi_base       = 0             'must be a multiple of 8
_clkfreq        = 285_000_000    '28.5 MHz pixel clock

' 354_689_500   200x Atari 8-bit PAL,  50x Amiga PAL  - real settings: 49,869-> 354_693_878  - 1.0000123 - mode 0 and 192
' 357_954_500   200x Atari 8-bit NTSC, 50x Amiga NTSC - real settings: 49,877-> 357_959_184  - 1.0000131 - mode 256

' 319_220_550   180x PAL  - real settings: 51,814-> 319_215_686 - 0.9999848 - mode 64
' 322_159_050   180x NTSC - real settings: 37,596-> 322_162_162 - 1.0000097 - mode 320 and 448

' 283_751_600   160x PAL  - real settings: 64,908-> 283_750_000 - 0.9999944 - mode 128
' 286_363_600   160x NTSC - real settings: 22,315-> 286_363_636 - 1.0000001 - mode 384

''--------- VGA DOS color definition constants

c_black         =    0
c_blue          =  117
c_green         =  199
c_cyan          =  149
c_red           =   39
c_magenta       =   71
c_brown         =  246
c_lightgray     =   10
c_darkgray      =    5
c_lightblue     =  121
c_lightgreen    =  203
c_lightcyan     =  155
c_lightred      =   43
c_lightmagenta  =   75
c_yellow        =  234
c_white         =   15

timingsxxt      =  $70810000

var

'----------  A pointer block, these pointers will be passed to the cog

long buf_ptr           'main buffer pointer
long vblank_ptr        'vblank signalling variable pointer
'ong cursor_ptr        'cursor position pointer
long mode_ptr          'graphics mode # pointer
long palette_ptr       '256-color palette pointer; bit 31 set: do not read
long dl_ptr            'display list pointer
long fontnum_ptr       'offset to font pointer : TODO - remove this, use byte #1 in the buffer
long hdmibase          'HDMI pin#

'----------

long cog               'driver cog#
long bordercolor       'border color
long buflen            'buffer length in longs
byte cursor_x          'X cursor position in text modes
byte cursor_y          'Y cursor position
byte cursor_sh         'cursor shape, 0 to 15, 0=full rectangle..15-one line, >=16 no cursor
byte dummyalign        'alignment dummy byte
long write_color       'character color for write/writeln
long write_background  'background color for write/writeln
long vblank            'vblank signalling
byte n_string[12]      'string buffer for inttostr and hextostr
long timings[16]       'graphic mode timings
long cpl               'char/pixels per line

long lines             'screen text lines count
long fontnum           'offset to font
long streamer[6]       'streamer constants
byte colors[16]        'vga colors
long graphmode
long font_family

long xzoom, yzoom, azoom



''--------- timings
' 0  m_bs        - before sync,             16      80
' 1  m_sn        - sync                     80     160
' 2  m_bv        - before visible           20      84
' 3  m_vi        - visible                1024     816
' 4  m_border    - left and right borders  112       8
' 5  m_lut1      - pixels per char           8       8
' 6  i_vborder   - up and down border       48       8
' 7  i_upporch   - up non visible           16       8
' 8  i_vsync     - vsync                    16      12
' 9  i_downporch - down non visible         16       8
'10  mode #
'11  cpl         - character per line
'12  scanlines #
'13  clock
'14  hubset value for clock settings


'*************************************************************************
'                                                                        *
'  A dummy start function if someone runs this driver alone              *
'                                                                        *
'*************************************************************************

pub dummy()|i,x,y,ntsc,bbb,ccc,zzx,zzy,amode,x1,x2,y1,y2,r
'' this is not a main program.

start(512+0+48,hdmi_base)

cls(154,147)
'setfontfamily(0)
waitvbl(100)
outtextxycg(0,0,inttostr(long[0]),154,147)
waitvbl(300)
repeat i from 0 to 10000
  ccc:=getrnd()&255
  x1:=getrnd()+//240
  x2:=getrnd()+//240
  y1:=getrnd()+//400
  y2:=getrnd()+//400


  line(x1,y1,x2,y2,ccc)

repeat i from 0 to 1000

  x1:=getrnd()+//240
  y1:=getrnd()+//400
  r:=getrnd()+//50
  ccc:=getrnd()&255
  fcircle(x1,y1,r,ccc)   

repeat i from 0 to 10000
  ccc:=getrnd()&255
  x1:=getrnd()+//240
  x2:=getrnd()+//240
  y1:=getrnd()+//400
  y2:=getrnd()+//400


  frame(x1,y1,x2,y2,ccc)
  
repeat i from 0 to 10000

  x1:=getrnd()+//240
  y1:=getrnd()+//4400
  r:=getrnd()+//50
  ccc:=getrnd()&255
  circle(x1,y1,r,ccc) 
  
repeat i from 0 to 1000
  ccc:=getrnd()&255
  x1:=getrnd()+//240
  x2:=getrnd()+//50
  y1:=getrnd()+//400
  y2:=getrnd()+//50


  box(x1,y1,x1+x2,y1+y2,ccc)  

repeat

'------------------------------

pub fcircle(x0,y0,r,c) | d,x,y,da,db

d:=5-4*r
x:=0
y:=r
da:=(-2*r+5)*4
db:=3*4
repeat while (x<=y) 
  line(x0-x,y0-y,x0+x,y0-y,c)
  line(x0-x,y0+y,x0+x,y0+y,c)
  line(x0-y,y0-x,x0+y,y0-x,c)
  line(x0-y,y0+x,x0+y,y0+x,c)
  if d>0 
    d+=da
    y-=1
    x+=1
    da+=4*4
    db+=2*4
  else
    d+=db
    x+=1
    da+=2*4
    db+=2*4
 
  
pub circle(x0,y0,r,c) | d,x,y,da,db

 
d:=5-4*r
x:=0
y:=r
da:=(-2*r+5)*4
db:=3*4
repeat while (x<=y) 
  putpixel(x0-x,y0-y,c)
  putpixel(x0-x,y0+y,c)
  putpixel(x0+x,y0-y,c)
  putpixel(x0+x,y0+y,c)
  putpixel(x0-y,y0-x,c)
  putpixel(x0-y,y0+x,c)
  putpixel(x0+y,y0-x,c)
  putpixel(x0+y,y0+x,c)
  if d>0 
    d+=da
    y-=1
    x+=1
    da+=4*4
    db+=2*4
  else
    d+=db
    x+=1
    da+=2*4
    db+=2*4


pub frame(x1,y1,x2,y2,c)

line(x1,y1,x2,y1,c)
line(x1,y2,x2,y2,c)
line(x1,y1,x1,y2,c)
line(x2,y1,x2,y2,c)

pub box(x1,y1,x2,y2,c) |yy

repeat yy from y1 to y2
  line(x1,yy,x2,yy,c)

pub line(x1,y1,x2,y2,c) | d,dx,dy,ai,bi,xi,yi,x,y

x:=x1
y:=y1

if (x1<x2) 
  xi:=1
  dx:=x2-x1
else
  xi:=-1
  dx:=x1-x2
  
if (y1<y2) 
  yi:=1
  dy:=y2-y1
else
  yi:=-1
  dy:=y1-y2

putpixel(x,y,c)

if (dx>dy)
  ai:=(dy-dx)*2
  bi:=dy*2
  d:= bi-dx
  repeat while (x<>x2) 
    if (d>=0) 
      x+=xi
      y+=yi
      d+=ai
    else
      d+=bi
      x+=xi
    putpixel(x,y,c)
else
  ai:=(dx-dy)*2
  bi:=dx*2
  d:=bi-dy
  repeat while (y<>y2)
    if (d>=0)
      x+=xi
      y+=yi
      d+=ai
    else
      d+=bi
      y+=yi
    putpixel(x, y,c)

pub putcharxycf(x,y,achar,f) |xx, yy,bb

repeat yy from 0 to 15
  bb:=byte[@vga_font+font_family<<10+achar<<4+yy]
  repeat xx from 0 to 7
    if (bb&(1<<xx))<>0
      putpixel(xx+x,yy+y,f)

pub putcharxycg(x,y,achar,f,b) |xx, yy,bb

repeat yy from 0 to 15
  bb:=byte[@st_font+achar<<4+yy]
  repeat xx from 0 to 7
    if (bb&(1<<xx))<>0
      putpixel(xx+x,yy+y,f)
    else
      putpixel(xx+x,yy+y,b)

pub putcharxycz(x,y,achar,f,b,xz,yz) |xx,xxx,yy,yyy,bb

repeat yy from 0 to 15
  bb:=byte[@vga_font+font_family<<10+achar<<4+yy]
  repeat xx from 0 to 7
    if (bb&(1<<xx))<>0
      repeat yyy from 0 to yz-1
        repeat xxx from 0 to xz-1
          putpixel(xz*xx+xxx+x,yz*yy+yyy+y,f)
    else
      repeat yyy from 0 to yz-1
        repeat xxx from 0 to xz-1
          putpixel(xz*xx+xxx+x,yz*yy+yyy+y,b)

pub outtextxycg(x,y,text,f,b) | iii,c

repeat iii from 0 to strsize(text)-1
  putcharxycg(x+8*iii,y,byte[text+iii],f,b)

pub outtextxycf(x,y,text,f) | iii,c

repeat iii from 0 to strsize(text)-1
  putcharxycf(x+8*iii,y,byte[text+iii],f)

pub outtextxycz(x,y,text,f,b,xz,yz) | iii,c

repeat iii from 0 to strsize(text)-1
  putcharxycz(x+8*xz*iii,y,byte[text+iii],f,b,xz,yz)


''---------- putpixel - put a pixel on the screen

pub putpixel(x,y,c)

if ((y>=0) & (y<4*cpl) & (x>=0) & (x<lines))
    byte[buf_ptr+4*cpl*(lines-x-1)+y]:=c
    

'**********************************************************************r***
'                                                                        *
' Font related functions                                                 *
'                                                                        *
'*************************************************************************

''--------- Redefine a character

pub defchar(fn,ch,b0,b1,b2,b3,b4,b5,b6,b7,b8,b9,b10,b11,b12,b13,b14,b15) :s

s:=@st_font+fn+ch*16
byte[s+00]:=b0
byte[s+01]:=b1
byte[s+02]:=b2
byte[s+03]:=b3
byte[s+04]:=b4
byte[s+05]:=b5
byte[s+06]:=b6
byte[s+07]:=b7
byte[s+08]:=b8
byte[s+09]:=b9
byte[s+10]:=b10
byte[s+11]:=b11
byte[s+12]:=b12
byte[s+13]:=b13
byte[s+14]:=b14
byte[s+15]:=b15

'*************************************************************************
'                                                                        *
'  Cursor functions                                                      *
'                                                                        *
'*************************************************************************

''---------- Set the (x,y) position of cursor

cursor_x:=x
cursor_y:=y


'*************************************************************************
'                                                                        *
'  VBlank functions                                                      *
'                                                                        *
'*************************************************************************

pub waitvbl1() 'reentrant

repeat until vblank==0
repeat until vblank==1

pub waitvbl(amount) | i

''---------- Wait for start of vblank. Amount=delay in frames

repeat i from 1 to amount
  repeat until vblank==0
    waitus(100)
  repeat until vblank==1
    waitus(100)


pub waitvblend(amount) | i

''---------- Wait for end of vblank. Amount=delay in frames

repeat i from 1 to amount
  repeat until vblank==1
    waitus(100)
  repeat until vblank==0
    waitus(100)

'*************************************************************************
'                                                                        *
'  Color functions                                                       *
'                                                                        *
'*************************************************************************

''---------- Get a VGA color code

pub getvgacolor(color):r

return colors[color]

pub getpalettecolor(color):r

return long[@ataripalette+4*color]


''---------- Clear the screen, set its foreground/background color

pub cls(fc,bc)   :c

c:=bc
bytefill(buf_ptr,c,buflen*4)

cursor_x:=0
cursor_y:=0


'*************************************************************************
'                                                                        *
'  Conversions                                                           *
'                                                                        *
'*************************************************************************

''---------- Convert a integer to dec string, return a pointer

pub inttostr(i):result |q,pos,k,j

j:=i
pos:=10
k:=0

if (j==0)
  n_string[0]:=48
  n_string[1]:=0

else
  if (j<0)
    j:=0-j
    k:=45

  n_string[11]:=0
  repeat while (pos>-1)
    q:=j//10
    q:=48+q
    n_string[pos]:=q
    j:=j/10
    pos-=1
  repeat while n_string[0]==48
    bytemove(@n_string,@n_string+1,12)

  if k==45
     bytemove(@n_string+1,@n_string,12)
     n_string[0]:=k

q:=@n_string
return q

''----------  Convert unsigned integer to hex string with d digits, return a pointer

pub inttohex(i,d):result |q,pos,k,j

j:=i
pos:=d-1
k:=0
n_string[d]:=0
repeat k from 0 to d-1
  n_string[k]:=48
if (j<>0)

  repeat while (pos>-1)
    q:=j+//16
    if (q>9)
      q:=q+7
    q:=48+q
    n_string[pos]:=q
    j:=j+/16
    pos-=1

q:=@n_string
return q



'*************************************************************************
'                                                                        *
'  Mode setting and driver start                                                          *
'                                                                        *
'*************************************************************************

''---------  Set the graphics mode



pub setmode(mode) | i

longmove(@timings,@timings000,16)   'PAL 50 Hz signaling 1140x624, active 800x480, 100x30 text, wide border, 2 colors per pixel, 00_00_00_00_00=0, 354_693_878 Hz

timings[5]:=4
timings[11]:=timings[11]<<1
palette_ptr:=@ataripalette

repeat i from 0 to 4
  timings[i]:=timings[i]+hdmibase<<17+ timingsxxt
timings[5]:=timings[5]+hdmibase<<17
clkfreq:=timings[13]
waitms(1)

xzoom:=2
yzoom:=2
azoom:=xzoom*yzoom

cpl:=timings[11]

lines:=timings[12]/yzoom
cpl:=cpl/xzoom

buflen:=(cpl*lines)

buf_ptr:=$80000-4*buflen
mode_ptr:=@timings
graphmode:=mode




      
'--------- Start the driver with graphics mode 'mode' at pins 'base'

pub start(mode,base):result


hdmibase:=base

' the mode has to be set here to enable computing the buffer length

setmode(mode)

vblank_ptr:=@vblank


fontnum_ptr:=@fontnum

fontnum:=0  ' PC type font ' TODO: font# in buffer byte #1
bytemove(@colors,@vgacolors,16)

' initialize a cursor

cursor_x:=0
cursor_y:=0
cursor_sh:=14


' start the cog

cog:=coginit(16,@hdmi, @buf_ptr)
waitms(20)

' clear the screen and set the colors to green on black

return cog

'**********************************************************************************
'
'        Fonts and palettes
'
'**********************************************************************************
dat
st_font        file "st4font.def"
ataripalette   file "ataripalettep2.def"
'**********************************************************************************
'
'        Timings and colors definitions
'
'**********************************************************************************

   'streamer sets for text mode, to add to timings[0..5]


'                     bf.hs, hs,  bf.vis  visible, lr bord, pixel, ud bord,  up p., vsync, down p., mode, cpl, scanlines,  clock,                  hubset                  total vis lines
timings000      long   48,   32,    80,     800,      0,     8,     0 ,      3,      7,    6,      0,  100,   480,     285000000,   %1_110000__11_0110_1100__1111_1011,   480
                           
vgacolors       byte   0, 117, 199, 151, 39, 71, 246, 10, 5, 121, 203, 155, 43, 75, 234, 15


'**********************************************************************************
'
'        PASM driver code
'
'**********************************************************************************

DAT             org

hdmi            setq    #10
                rdlong  framebuf,  ptra                  'read pointers
                setq    #15
                rdlong  m_bs,modeptr   
                setq2   #255
                rdlong  $000, paletteptr 

                wrlong  #aend,#0                         'write driver length to hub#0: DEBUG/demo
                setcmod #$100                            'enable HDMI mode
                mov     ii,#448                          '7 << 6          
                add     ii,hbase
                drvl    ii                                 '#7<<6 + hdmi_base      ' enable HDMI pins
                wrpin   ##%10110_1111_0111_10_00000_0, ii  '#7<<6 + hdmi_base      ' a '123 ohm BITDAC for pins

                setxfrq ##$0CCCCCCC+1                   'set streamer freq to 1/10th clk


''--------  frame rendering main loop  ---------------------------------------------------

p101            add     frames,#1
                mov     framebuf2,framebuf
                mov     hsync0,sync_000                  '
                mov     hsync1,sync_001
                callpa  i_upporch ,#blank
                wrlong  #0,vblankptr
                mov     ii,#0
                
                mov linenum,#0
p301            call    #hsync                          ' now call hsync to gain some time between xconts
                incmod  ii,#1 wc
           if_c sub framebuf2,#400     


'----------------------------------------- display pixels

                mov     m_lut2,m_lut1                   ' m_lut1 loaded from hub timing block, pixel per long
                mov     cpl2,i_cpl                      ' i_cpl in graphic modes is longs per line
                add     m_lut2,lutg8                    ' 8 bpp modes
 
                
                rep #4,#200 
                rdlong  char,framebuf2
                add     framebuf2,#2              
                movbyts char,#%01010000          
                xcont   m_lut2,char              

p302            add     linenum,#1
                add     dlptr2,#4
                cmp     linenum,i_totalvis  wz
         if_nz  jmp     #p301

p112            wrlong  #1,vblankptr
                callpa  i_downporch ,#blank             'bottom blanks

                mov     hsync0,sync_222                 'vsync on
                mov     hsync1,sync_223
                callpa  i_vsync,#blank                  'vertical sync blanks
                jmp     #p101

'' -------------------------------------- END of graph  line ---------------------------------


blank           call    #hsync                          'blank lines
                xcont   m_vi,hsync0
        _ret_   djnz    pa,#blank

hsync           xcont   m_bs,hsync0                     'horizontal sync
                xzero   m_sn,hsync1
        _ret_   xcont   m_bv,hsync0

'' consts and vars

sync_000        long    %1101010100_1101010100_1101010100_10    '
sync_001        long    %1101010100_1101010100_0010101011_10    '        hsync
sync_222        long    %0101010100_0101010100_0101010100_10    'vsync
sync_223        long    %0101010100_0101010100_1010101011_10    'vsync + hsync

'border          long    %00000000_00011010_00101100_00000000

'------ these longs will be set by setmode function

m_bs            long    0        'blanks before sync
m_sn            long    0        'sync
m_bv            long    0        'blanks before visible
m_vi            long    0        'visible pixels #
m_border        long    0        'left/right borders
m_lut1          long    0        'characters
i_vborder       long    0        'up/down borders
i_upporch       long    0        'up porch lines
i_vsync         long    0        'vsync lines
i_downporch     long    0        'down porch lines
i_modenum       long    0        'mode #
i_cpl           long    0        'chars/longs per line
i_lines         long    0        'scanlines #
i_clock         long    0
i_hubset        long    0
i_totalvis      long    0

'-------------------------------------

m_lut2          long    0

colordepth
linestart       long    0
linenum         long    0
lutaddr         long    256

cursorsh        long    14
frames          long    0
cursorx         long    0
cursory         long    0
cursorpos       long    0
cursorpos2      long    0
fontstart       long    0
border2         long 0
lutiv           long $70810000
lutt1           long $00880000
lutg1           long $00800000
lutg2           long $10800000
lutg4           long $20800000
lutg8           long $30800000
framebuf        long 0
'fontbuf         long 0
'borderptr       long 0
vblankptr       long 0
'cursorptr       long 0
modeptr         long 0
paletteptr      long 0
dlptr           long 0
fontnumptr      res     1
hbase           res     1
borderptr2      res     1
dlptr2 res 1

ii              res     1
framebuf2       res     1
hsync0          res     1
hsync1          res     1

cpl2 res 1
char            res     1

aend             long 0
                fit     496                     '





{{
------------ MIT License ---------------
}}
