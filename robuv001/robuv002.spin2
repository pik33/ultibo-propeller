'-------------------------------------------------------------------------------
'ROBUV driver v. 0.01-20210525
'Piotr Kardasz pik33@o2.pl
'
'uses code from 03_jm_sbus_serial_decode.spin2 by Jon "JonnyMac" McPhalen (MIT licensed)
'
'-------------------------------------------------------------------------------

con

	CLK_FREQ =354693878 '322162162                                     	' system freq as a constant
	
	_clkfreq = CLK_FREQ                                           	' set system clock
	SBUS_RX  = 16                                                 	' S.BUS serial input
 
	SBUS_CFG  = P_ASYNC_RX' do not invert | P_INVERT_IN                          	' inverted serial input
	SBUS_BAUD = (CLK_FREQ / 100_000) << 16 | (9-1)                	' 100K baud, 9 bits (8 data + parity)  
	baudrate        = 4000000
 rxpin           = 14
 txpin           = 12
 pisleep = 13
 pipower = 15
 pipower2 = 8
 adcpin  = 17  ' battery voltage 
  
obj

 	v: "hng025r3"
       adc:"adccog"
var

  	byte  frame[28]						      	' 25 bit sbus buffer, extended to 28 bytes (align)
  	word  channel[18]						' 16 channels
long rxp
long txp
long mrxp
long mtxp
long serialbuf[64]
long serialtail,serialfront
long serialstack[64]
long lidarstack[64]
long voltage


pub main() | t11, i11, h11,h12,h21,h22 ,c,d

v.start(512+48+192+5,0)
v.cls(154,147)
v.setfontfamily(4)
v.waitvbl(100)
v.outtextxycg(8,0,string("Starting RPi 0"),154,147)
pinh(pipower)
v.outtextxycg(8,16,string("Starting RPi 1"),154,147)
pinh(pipower2)
adc.start(adcpin)

'dacinit()
'dacset($8000,$8000)
cogspin(16,display_lidar(),@lidarstack)

repeat
  v.waitvbl(1)
  voltage:=adc.measure()
  c:=120
  if voltage<22200
    c:=200
  if voltage<20000
    c:=252
  if voltage<19000
    c:=40
    
  v.outtextxycg(8+0,32,string("Voltage: "),c,147)
  v.outtextxycg(8+72,32,v.inttostr(voltage+/1000),c,147)
  v.outtextxycg(8+88,32,string("."),c,147)  
  v.outtextxycg(8+96,32,v.inttostr(voltage+//1000),c,147)
  v.outtextxycg(8+128,32,string("V"),c,147)
  v.outtextxycg(8+128,32,string("V"),c,147)
  
  v.outtextxycg(8,48,string("SBUS:"),154,147)
  

  if (receive_sbus(SBUS_RX, @frame))                          ' get s.bus frame
    pintoggle(56)
    decode_sbus(@frame, @channel)                             ' convert to channel values
    repeat i11 from 0 to 15      
      d:=(8*(channel[i11]/1000))
      v.outtextxycg(16-d+(i11//4)<<6,16*(i11/4)+64,v.inttostr(channel[i11]),154,147)
 {{    
    h11:=channel[0] 
    h12:=channel[1]
     
 ' 1 joy do przodu, robot jedzie, 0203= h11=0   
    
    if (h11>1500) & (h12>300) & (h12<1500)
      h21:=$2800
      h22:=$2800

' 2. joy do tyłu, robot do tyłu

    elseif (h11<300) & (h12>300) & (h12<1500)
      h21:=$D800
      h22:=$D800

  '3,4  joy w bok
  
    elseif (h12>1500) & (h11<1500) & (h11>300)  ''lewo
      h21:=$2800
      h22:=$D800
    
    
    elseif (h12<300) & (h11<1500) & (h11>300)   ''prawo
      h21:=$D800
      h22:=$2800
    
    elseif (h12>1500) & (h11>1500) '' lewo góra   
      h21:=$2800
      h22:=$4000
    
    elseif (h12>1500) & (h11<300) ''  lewo dół
      h21:=$c000
      h22:=$D800  
    
    elseif (h12<300) & (h11>1500)    '' prawo góra
      h21:=$4000
      h22:=$2800

    elseif (h12<300) & (h11<300)  '' prawo dół
      h21:=$d800
      h22:=$c000  
      
    else
      h21:=$8000
      h22:=$8000  
 
    dacset(h21,h22)    
  
    if (channel[7]>1000)
      pinwrite(2,0)
      pinwrite(3,0)
    else
      pinwrite(2,1)
      pinwrite(3,1)

  }}
pub display_lidar()  | xxx,yyy,rr

serial_start(rxpin, txpin, baudrate)
rxflush()

cogspin(16,serialcog(),@serialstack)


xxx:=0 
yyy:=0

repeat
  repeat
    rr:=serialblockingread()
    if rr==255
      xxx:=0
      yyy:=0
    else  
      byte[256+v.buf_ptr+(xxx<<10)+2*yyy]:=rr
      byte[256+v.buf_ptr+(xxx<<10)+ 512+2*yyy]:=rr
      byte[256+v.buf_ptr+(xxx<<10)+2*yyy+1]:=rr
      byte[256+v.buf_ptr+(xxx<<10)+512+2*yyy+1]:=rr
      xxx++
      if (xxx>79)
        xxx:=0
        yyy:=(yyy+1)+//60
        
  
pub receive_sbus(p, p_sbus) : success | x, b, byte buffer[25]   ' blocks if no signal!!!

  pinclear(p)                                                   ' force to tri-state mode

  x := 0                                                        ' find inter-frame gap
  repeat
    if (pinread(p) == 1)'0
      if (++x == 250)
        quit
    else
      x := 0

  pinstart(p, SBUS_CFG, SBUS_BAUD, 0)                           ' configure for S.BUS serial

  repeat x from 0 to 24                                         ' get s.bus frame
    repeat until (pinread(p))                                   ' wait for byte
    b := rdpin(p) >> 23                                         ' get data + parity
    if (ones(b) & 1)                                            ' if odd parity
      return false                                              '  return error
    else                                                        ' else
      buffer[x] := b & $FF                                      '  save to buffer

  if (buffer[0] <> $0F) || (buffer[24] <> $00)                  ' check header & footer bytes
    return false

  bytemove(p_sbus, @buffer, 25)                                 ' success; copy to destination

  return true


pub decode_sbus(p_sbus, p_chans) | ch, work, bc, flags

  p_sbus += 1                                                   ' point to data byte #1

  longfill(@ch, 0, 3)                                           ' clear locals

  repeat while (ch <= 15)                                       ' Ch 1-16 decoding
    repeat while (bc < 11)                                      ' collect enough bits for channel
      work |= (byte[p_sbus++] << bc)                            ' get byte, position, add to work
      bc += 8                                                   ' bump bit count
    word[p_chans][ch++] := work & $07FF                         ' extract and save channel
    work >>= 11                                                 ' remove channel, re-align lsb
    bc -= 11                                                    ' fix bit count

  flags := byte[p_sbus]

  word[p_chans][16] := (flags & %0001) ? $07FF : 0              ' convert ch17 bit
  word[p_chans][17] := (flags & %0010) ? $07FF : 0              ' convert ch18 bit
 
pub dacinit() | dac

dac:=%10110_00000000_01_00011_0  


org

        wrpin   dac,#0
        wxpin   #256,#0

        wrpin   dac,#1
        wxpin   #256,#1

        dirh    #0 addpins 1
        
end    

   
pub dacset(lll,rrr)
   
org   
        wypin   lll,#0
        wypin   rrr,#1
end  


''---------------------- Serial functions from jm_serial.spin2

pub serial_start(rxpin2, txpin2, baud) | bitmode

'' Start simple serial coms on rxpin and txpin at baud

'  longmove(@rxp, @rxpin2, 2)                                     ' save pins

  rxp:=rxpin2
  txp:=txpin2

  bitmode := muldiv64(clkfreq, $1_0000, baud) & $FFFFFC00       ' set bit timing
  bitmode |= 7                                                  ' set bits (8)

  org
                fltl      rxpin2                                 ' configure rx smart pin
                wrpin     ##P_ASYNC_RX, rxpin2
                wxpin     bitmode, rxpin2
                drvl      rxpin2
                fltl      txpin2                                ' configure tx smart pin
                wrpin     ##(P_ASYNC_TX | P_OE), txpin2
                wxpin     bitmode, txpin2
                drvl      txpin2
  end



pub rxflush()

'' Clear serial input

  repeat
  while (rxcheck() >= 0)


pub rxcheck() : rxbyte | check

'' Check for serial input
'' -- returns -1 if nothing available

  rxbyte := -1
  check := pinr(rxp)
  if (check)
    rxbyte := rdpin(rxp) >> 24


pub rxtime(ms) : b | mstix, t

'' Wait ms milliseconds for a byte to be received
'' -- returns -1 if no byte received, $00..$FF if byte

  mstix := clkfreq / 1000

  t := getct()
  repeat
  until ((b := rxcheck()) >= 0) or (((getct() - t) / mstix) > ms)


pub rx() : rxbyte

'' Wait for serial input
'' -- blocks!

  repeat
    rxbyte := rxcheck()
  until (rxbyte >= 0)


pub tx(b)

'' Emit byte

  wypin(txp, b)
  txflush()

pub txflush() | check

'' Wait until last byte has finished

  repeat
    check := pinr(txp)
  while (check == 0)

  
  
pub serialcog()|q

serialfront:=0
serialtail:=0

repeat
  if serialfront<>((serialtail-1) //32)
    q:=rxcheck()
    if q>=0
     serialbuf[serialfront]:=q
     serialfront+=1
     serialfront:=serialfront // 32

pub serialread():r

if serialfront<>serialtail
  r:=serialbuf[serialtail]
  serialtail:=(serialtail+1)//32
else
  r:=-1
return r

pub serialblockingread(): r

repeat
  r:=serialread()
until r<>-1
return r


{{

  Terms of Use: MIT License

  Permission is hereby granted, free of charge, to any person obtaining a copy of this
  software and associated documentation files (the "Software"), to deal in the Software
  without restriction, including without limitation the rights to use, copy, modify,
  merge, publish, distribute, sublicense, and/or sell copies of the Software, and to
  permit persons to whom the Software is furnished to do so, subject to the following
  conditions:

  The above copyright notice and this permission notice shall be included in all copies
  or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
  INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
  PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
  CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
  OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

}}
