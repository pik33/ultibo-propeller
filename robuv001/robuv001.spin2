'-------------------------------------------------------------------------------
'ROBUV driver v. 0.01-20210525
'Piotr Kardasz pik33@o2.pl
'
'uses code from 03_jm_sbus_serial_decode.spin2 by Jon "JonnyMac" McPhalen (MIT licensed)
'
'-------------------------------------------------------------------------------

con

	CLK_FREQ = 322_159_050                                        	' system freq as a constant
	_clkfreq = CLK_FREQ                                           	' set system clock
	SBUS_RX  = 7                                                  	' S.BUS serial input
 
	SBUS_CFG  = P_ASYNC_RX | P_INVERT_IN                          	' inverted serial input
	SBUS_BAUD = (CLK_FREQ / 100_000) << 16 | (9-1)                	' 100K baud, 9 bits (8 data + parity)  

obj

 	v: "hng025"

var

  	byte  frame[28]						      	' 25 bit sbus buffer, extended to 28 bytes (align)
  	word  channel[18]						' 16 channels


pub main() | t11, i11, h11,h12,h21,h22

v.start(512+448+1+48,8)
v.cls(154,147)
v.setfontfamily(4)
v.waitvbl(100)
dacinit()
dacset($8000,$8000)
repeat
  v.waitvbl(1)
  if (receive_sbus(SBUS_RX, @frame))                          ' get s.bus frame
    decode_sbus(@frame, @channel)                             ' convert to channel values
    repeat i11 from 0 to 15                                     ' display channel values
      v.outtextxycg(0,16*i11,v.inttostr(i11),154,147)
      v.outtextxycg(88,16*i11,string(" "),154,147)      
      v.outtextxycg(64,16*i11,v.inttostr(channel[i11]),154,147)
     
    h11:=channel[0] 
    h12:=channel[1]
     
 ' 1 joy do przodu, robot jedzie, 0203= h11=0   
    
    if (h11>1500) & (h12>300) & (h12<1500)
      h21:=$2800
      h22:=$2800

' 2. joy do tyłu, robot do tyłu

    elseif (h11<300) & (h12>300) & (h12<1500)
      h21:=$D800
      h22:=$D800

  '3,4  joy w bok
  
    elseif (h12>1500) & (h11<1500) & (h11>300)  ''lewo
      h21:=$2800
      h22:=$D800
    
    
    elseif (h12<300) & (h11<1500) & (h11>300)   ''prawo
      h21:=$D800
      h22:=$2800
    
    elseif (h12>1500) & (h11>1500) '' lewo góra   
      h21:=$2800
      h22:=$4000
    
    elseif (h12>1500) & (h11<300) ''  lewo dół
      h21:=$c000
      h22:=$D800  
    
    elseif (h12<300) & (h11>1500)    '' prawo góra
      h21:=$4000
      h22:=$2800
    
    elseif (h12<300) & (h11<300)  '' prawo dół
      h21:=$d800
      h22:=$c000  
      
    else
      h21:=$8000
      h22:=$8000  
 
    dacset(h21,h22)    
  
    if (channel[7]>1000)
      pinwrite(2,0)
      pinwrite(3,0)
    else
      pinwrite(2,1)
      pinwrite(3,1)

  
  
pub receive_sbus(p, p_sbus) : success | x, b, byte buffer[25]   ' blocks if no signal!!!

  pinclear(p)                                                   ' force to tri-state mode

  x := 0                                                        ' find inter-frame gap
  repeat
    if (pinread(p) == 0)
      if (++x == 250)
        quit
    else
      x := 0

  pinstart(p, SBUS_CFG, SBUS_BAUD, 0)                           ' configure for S.BUS serial

  repeat x from 0 to 24                                         ' get s.bus frame
    repeat until (pinread(p))                                   ' wait for byte
    b := rdpin(p) >> 23                                         ' get data + parity
    if (ones(b) & 1)                                            ' if odd parity
      return false                                              '  return error
    else                                                        ' else
      buffer[x] := b & $FF                                      '  save to buffer

  if (buffer[0] <> $0F) || (buffer[24] <> $00)                  ' check header & footer bytes
    return false

  bytemove(p_sbus, @buffer, 25)                                 ' success; copy to destination

  return true


pub decode_sbus(p_sbus, p_chans) | ch, work, bc, flags

  p_sbus += 1                                                   ' point to data byte #1

  longfill(@ch, 0, 3)                                           ' clear locals

  repeat while (ch <= 15)                                       ' Ch 1-16 decoding
    repeat while (bc < 11)                                      ' collect enough bits for channel
      work |= (byte[p_sbus++] << bc)                            ' get byte, position, add to work
      bc += 8                                                   ' bump bit count
    word[p_chans][ch++] := work & $07FF                         ' extract and save channel
    work >>= 11                                                 ' remove channel, re-align lsb
    bc -= 11                                                    ' fix bit count

  flags := byte[p_sbus]

  word[p_chans][16] := (flags & %0001) ? $07FF : 0              ' convert ch17 bit
  word[p_chans][17] := (flags & %0010) ? $07FF : 0              ' convert ch18 bit
 
pub dacinit() | dac

dac:=%10110_00000000_01_00011_0  


org

        wrpin   dac,#0
        wxpin   #256,#0

        wrpin   dac,#1
        wxpin   #256,#1

        dirh    #0 addpins 1
        
end    

   
pub dacset(lll,rrr)
   
org   
        wypin   lll,#0
        wypin   rrr,#1
end  

{{

  Terms of Use: MIT License

  Permission is hereby granted, free of charge, to any person obtaining a copy of this
  software and associated documentation files (the "Software"), to deal in the Software
  without restriction, including without limitation the rights to use, copy, modify,
  merge, publish, distribute, sublicense, and/or sell copies of the Software, and to
  permit persons to whom the Software is furnished to do so, subject to the following
  conditions:

  The above copyright notice and this permission notice shall be included in all copies
  or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
  INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
  PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
  CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
  OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

}}
