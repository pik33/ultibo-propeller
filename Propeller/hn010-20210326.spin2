'********************************************************************************************
' Nostalgic HDMI for P2 - 1 cog version
' Version 0.10 alpha -24.03.2021
' 1 cog, 100x30 text with border, 8x16 font
' (c) 2012-2021 Piotr Kardasz pik33@o2.pl
' MIT license: see end of file
'********************************************************************************************

CON


hdmi_base       = 48             'must be a multiple of 8
_clkfreq        = 200_000_000    'start safe, hubset will do the rest. The clock will be changed to:

' 354_689_500   200x Atari 8-bit PAL,  50x Amiga PAL  - real settings: 49,869-> 354_693_878  - 1.0000123 - mode 0 and 192
' 357_954_500   200x Atari 8-bit NTSC, 50x Amiga NTSC - real settings: 49,877-> 357_959_184  - 1.0000131 - mode 256

' 319_220_550   180x PAL  - real settings: 51,814-> 319_215_686 - 0.9999848 - mode 64
' 322_159_050   180x NTSC - real settings: 37,596-> 322_162_162 - 1.0000097 - mode 320 and 448

' 283_751_600   160x PAL  - real settings: 64,908-> 283_750_000 - 0.9999944 - mode 128
' 286_363_600   160x NTSC - real settings: 22,315-> 286_363_636 - 1.0000001 - mode 384

''--------- VGA DOS color definition constants

c_black         =    0
c_blue          =  117
c_green         =  199
c_cyan          =  151
c_red           =   39
c_magenta       =   71
c_brown         =  246
c_lightgray     =   10
c_darkgray      =    5
c_lightblue     =  123
c_lightgreen    =  203
c_lightcyan     =  155
c_lightred      =   43
c_lightmagenta  =   75
c_yellow        =  234
c_white         =   15

var

'----------  A pointer block, these pointers will be passed to the cog

long buf_ptr           'main buffer pointer 
long font_ptr          'font definition pointer 
long border_ptr        'border colors buffer pointer 
long vblank_ptr        'vblank signalling variable pointer
long cursor_ptr        'cursor position pointer 
long mode_ptr          'graphics mode # pointer 
long palette_ptr       '256-color palette pointer 
long command_ptr       'command variable (mailbox interface, used to change a palette color) pointer 
long fontnum_ptr       'offset to font pointer : TODO - remove this, use byte #1 in the buffer 

'----------

long hdmibase          'HDMI pin#
long cog               'driver cog#
long bordercolor[38]   'border colors, TODO: move this to the buffer
long buflen            'buffer length in longs
byte cursor_x          'X cursor position in text modes
byte cursor_y          'Y cursor position
byte cursor_sh         'cursor shape, 0 to 15, 0=full rectangle..15-one line, >=16 no cursor
byte dummyalign        'alignment dummy byte
long write_color       'character color for write/writeln
long write_background  'background color for write/writeln
long vblank            'vblank signalling
byte n_string[12]      'string buffer for inttostr and hextostr
long timings[16]       'graphic mode timings
long cpl               'char/pixels per line
long lines             'screen text lines count 
long cmd1              'cmd long #1 (command) - 0..255->set color # to cmd2, 257 idle; to be changed if more commands needed
long cmd2              'cmd long #2 (parameter)
long fontnum           'offset to font
long streamer[6]       'streamer constants
byte colors[16]        'vga colors

''--------- timings
' 0  m_bs        - before sync,             16      80
' 1  m_sn        - sync                     80     160
' 2  m_bv        - before visible           20      84
' 3  m_vi        - visible                1024     816
' 4  m_border    - left and right borders  112       8
' 5  m_lut1      - pixels per char           8       8
' 6  i_vborder   - up and down border       48       8
' 7  i_upporch   - up non visible           16       8
' 8  i_vsync     - vsync                    16      12
' 9  i_downporch - down non visible         16       8
'10  mode #
'11  cpl         - character per line
'12  scanlines #
'13  clock
'14  hubset value for clock settings


'*************************************************************************
'                                                                        *
'  A dummy start function if someone runs this driver alone              *
'                                                                        *
'*************************************************************************

pub dummy()
'' this is not a main program.

start(0,48)
writeln(string("Hello, World!"))
writeln(string("You started the driver instead of your main program!"))
cogstop(cogid)

'*************************************************************************
'                                                                        *
' Font related functions                                                 *
'                                                                        *
'*************************************************************************

''--------- Set a font offset. TODO: rremove, use byte#1 instead

pub setfontnum(afontnum)

fontnum:=afontnum

''--------- Redefine a character

pub defchar(fn,ch,b0,b1,b2,b3,b4,b5,b6,b7,b8,b9,b10,b11,b12,b13,b14,b15) :s

s:=@st_font+fn+ch*16
byte[s+00]:=b0
byte[s+01]:=b1
byte[s+02]:=b2
byte[s+03]:=b3
byte[s+04]:=b4
byte[s+05]:=b5
byte[s+06]:=b6
byte[s+07]:=b7
byte[s+08]:=b8
byte[s+09]:=b9
byte[s+10]:=b10
byte[s+11]:=b11
byte[s+12]:=b12
byte[s+13]:=b13
byte[s+14]:=b14
byte[s+15]:=b15


'*************************************************************************
'                                                                        *
'  Cursor functions                                                      *
'                                                                        *
'*************************************************************************

pub cursoron()

''---------- Switch the cursor on

cursor_sh+=16

pub cursoroff()

''---------- Switch the cursor on

cursor_sh-=16

pub setcursorpos(x,y)

''---------- Set the (x,y) position of cursor

cursor_x:=x
cursor_y:=y

pub setcursorshape(shape)

''---------- Define a cursor shape (0-full..15-line)

cursor_sh:=shape

'*************************************************************************
'                                                                        *
'  VBlank functions                                                      *
'                                                                        *
'*************************************************************************

pub waitvbl(amount) | i

''---------- Wait for start of vblank. Amount=delay in frames

repeat i from 1 to amount
  repeat until vblank==0
    waitus(100)
  repeat until vblank==1
    waitus(100)


pub waitvblend(amount) | i

''---------- Wait for end of vblank. Amount=delay in frames

repeat i from 1 to amount
  repeat until vblank==1
    waitus(100)
  repeat until vblank==0
    waitus(100)

'*************************************************************************
'                                                                        *
'  Color functions                                                       *
'                                                                        *
'*************************************************************************

pub setscreencolors(ff,bb) | c ,i

''---------- Set font and back colors for all screen - from 256 color palette

c:=ff<<24+bb<<16

repeat i from 0 to buflen-1
   long[buf_ptr+4*i]:= (long[buf_ptr+4*i] & $FFFF) |  c


pub setbordercolors(r,g,b) |color, i

''---------- Set border color for all screen - rgb

color:=r<<16+g<<8+b  ''' todo!!! border buffer has to be defined like a main buffer

repeat i from 0 to lines+1
  bordercolor[i]:=color


''---------- Set the color for the character at line y and position x

pub setcharcolor(x,y,c) | place, color

place:=(x)+(y*cpl)

if (place>buflen-1)
  place:=buflen-1

color:=(long[buf_ptr+4*place] &$FFFFFF) | (c<<24)
long[buf_ptr+4*place]:=color

''---------- Set the background color at line y and position x

pub setbackcolor(x,y,c) | place, color

place:=(x)+(y*cpl)

if (place>buflen-1)
  place:=buflen-1

color:=(long[buf_ptr+4*place] &$FF00FFFF) | (c<<16)
long[buf_ptr+4*place]:=color

''---------- Set the border color, o is upper border, lines+1 is lower border

pub setbordercolor(line,r,g,b) | color

color:=r<<16+g<<8+b
bordercolor[line]:=color

''---------- Set colors for putchar, write and writeln

pub setwritecolors(ff,bb)


write_color:=ff
write_background:=bb

''---------- Set color #c in palette to r,g,b

pub setcolor(c,r,g,b)  |cc

cc:=r<<24+g<<16+b<<8
cmd2:=cc
cmd1:=c
repeat until cmd1==257


'*************************************************************************
'                                                                        *
'  Text functions                                                        *
'                                                                        *
'*************************************************************************

''---------- Clear the screen, set its foreground/background color

pub cls(fc,bc)   :c

c:=fc<<24+bc<<16+$20
longfill(buf_ptr,c,buflen)
 setwritecolors(fc,bc)

''---------- Output a char at x,y, don't change colors and cursor position

pub putcharxy(x,y,achar) | c

c:=write_color<<24+write_background<<16
long[buf_ptr+4*(cpl*y+x)]:=long[4*(buf_ptr+cpl*y+x)] &$FFFF | c
long[buf_ptr+4*(cpl*y+x)]:=(long[4*(buf_ptr+cpl*y+x)] & $FFFFFF00)  | (achar & $000000FF)

''---------- Output a char at x,y and colors f,b, don't change a cursor position

pub putcharxyc(x,y,achar,f,b) | c

c:=f<<24+b<<16
long[buf_ptr+4*(cpl*y+x)]:=long[4*(buf_ptr+cpl*y+x)] &$FFFF | c
long[buf_ptr+4*(cpl*y+x)]:=(long[4*(buf_ptr+cpl*y+x)] & $FFFFFF00)  | (achar & $000000FF)

''--------- Output a string at position x,y without changing colors

pub outtextxy(x,y,text) | iii

repeat iii from 0 to strsize(text)-1
  long[buf_ptr+4*(cpl*y+x+iii)]:=(long[buf_ptr+4*(cpl*y+x+iii)] & $FFFFFF00)  | byte[text+iii]
  

''--------- Output a string at position x,y and colors b,f  

pub outtextxyc(x,y,text,f,b) | iii,c

c:=f<<24+b<<16
repeat iii from 0 to strsize(text)-1
  long[buf_ptr+4*(cpl*y+x+iii)]:=(long[buf_ptr+4*(cpl*y+x+iii)] & $FFFFFF00)  | byte[text+iii]
  long[buf_ptr+4*(cpl*y+x+iii)]:=(long[buf_ptr+4*(cpl*y+x+iii)] & $0000FFFF)  | c
  
''---------- Output a char at the cursor position, move the cursor  

pub putchar(achar) | c

c:=write_color<<24+write_background<<16
long[buf_ptr+4*(cpl*cursor_y+cursor_x)]:=long[4*(buf_ptr+cpl*cursor_y+cursor_x)] &$FFFF | c
long[buf_ptr+4*(cpl*cursor_y+cursor_x)]:=(long[4*(buf_ptr+cpl*cursor_y+cursor_x)] & $FFFFFF00)  | (achar & $000000FF)
cursor_x+=1
if cursor_x==cpl
  cursor_x:=0
  cursor_y+=1
  if cursor_y>lines-1
    scrollup()
    cursor_y:=lines-1

''--------- Output a string at the cursor position, move the cursor

pub write(text) | iii,c,ncx,ncy

c:=write_color<<24+write_background<<16
'debug(uhex_long(write_color))
ncy:=cursor_y
ncx:=cursor_x+strsize(text)
repeat while ncx>cpl-1
  ncx-=cpl
  ncy+=1
repeat while ncy>lines-1
  ncy-=1
  scrollup()
  cursor_y-=1

repeat iii from 0 to strsize(text)-1

  long[buf_ptr+4*(cpl*cursor_y+cursor_x+iii)]:=long[buf_ptr+4*(cpl*cursor_y+cursor_x+iii)] &$FFFF | c

outtextxy(cursor_x,cursor_y,text)

cursor_x:=ncx
cursor_y:=ncy

'--------- Output a string at the cursor position x,y, move the cursor to the next line

pub writeln(text)

write(text)
cursor_x:=0
cursor_y+=1
if (cursor_y>lines-1)
  scrollup()
  cursor_y:=lines-1

''-----------  Scroll the screen one line up

pub scrollup() | i

longmove(buf_ptr,buf_ptr+4*cpl,buflen-cpl)
repeat i from buflen-cpl to buflen-1
  long[buf_ptr+4*i]:=(long[buf_ptr+4*i] & $FFFF0000) | 32
  
''----------- Scroll the screen one line down  

pub scrolldown() | i

longmove(buf_ptr+cpl*4,buf_ptr,buflen-cpl)
repeat i from 0 to cpl-1
  long[buf_ptr+4*i]:=(long[buf_ptr+4*i] & $FFFF0000) | 32

''----------- Set cursor at the first character in a new line, scroll if needed

pub crlf()

cursor_x:=0
cursor_y+=1
if cursor_y>lines-1
  scrollup()
  cursor_y:=lines-1

''---------- Backspace. Move the cursor back, clear a character

pub bksp()

cursor_x-=1
if cursor_x==255
  cursor_x:=cpl-1
  cursor_y-=1
  if cursor_y==255
    cursor_y:=0
    scrollup()

outtextxy(cursor_x,cursor_y,string(" "))


'*************************************************************************
'                                                                        *
'  Conversions                                                           *
'                                                                        *
'*************************************************************************

''---------- Convert a integer to dec string, return a pointer

pub inttostr(i):result |q,pos,k,j

j:=i
pos:=10
k:=0

if (j==0)
  n_string[0]:=48
  n_string[1]:=0

else
  if (j<0)
    j:=0-j
    k:=45

  n_string[11]:=0
  repeat while (pos>-1)
    q:=j//10
    q:=48+q
    n_string[pos]:=q
    j:=j/10
    pos-=1
  repeat while n_string[0]==48
    bytemove(@n_string,@n_string+1,12)

  if k==45
     bytemove(@n_string+1,@n_string,12)
     n_string[0]:=k

q:=@n_string
return q

''----------  Convert unsigned integer to hex string with d digits, return a pointer

pub inttohex(i,d):result |q,pos,k,j

j:=i
pos:=d-1
k:=0
n_string[d]:=0
repeat k from 0 to d-1
  n_string[k]:=48
if (j<>0)

  repeat while (pos>-1)
    q:=j+//16
    if (q>9)
      q:=q+7
    q:=48+q
    n_string[pos]:=q
    j:=j+/16
    pos-=1

q:=@n_string
return q


'*************************************************************************
'                                                                        *
'  Mode setting and driver start                                                          *
'                                                                        *
'*************************************************************************

''---------  Set the graphics mode 

pub setmode(mode) | i

'' mode:nt_bb_cc_vv_hh
'' hh - h.zoom, vv-v.zoom, cc-c.depth, 
'' bb - borders/total pixels, 00-wide/1140, 01 medium/1026, 10 narrow/912, 11 no border, 1024 px PAL, 880 px NTSC

if (mode<512)  '' text modes
  longmove(@streamer,@timingsxxt,6)
  if (mode==0)                                'PAL 50 Hz signaling 1140x624, active 800x480, 100x30 text, wide border, 2 colors per pixel, 00_00_00_00_00=0, 354_693_878 Hz
    longmove(@timings,@timings000,16)
  if (mode==64)                               'PAL 50 Hz signaling 1026x624, active 880x496, 110x31 text, medium border, 2 colors per pixel 00_01_00_00_00=64, 319_215_686 Hz
    longmove(@timings,@timings064,16)
  if (mode==128)                              'PAL 50 Hz signaling 912x624, active 800x480, 100x30 text, medium border, 2 colors per pixel 00_10_00_00_00=64, 283750000z
    longmove(@timings,@timings128,16)
  if (mode==192)                              'PAL 50 Hz signaling 1140x624, active 1024x576, 128x36 text, borderless, 2 colors per pixel 00_11_00_00_00=6192, 354_693_878 Hz
    longmove(@timings,@timings192,16)
  if (mode==256)                              'NTSC 50 Hz signaling 1140x524, active 800x480, 100x30 text, NO PLACE FOR wide border, 2 colors per pixel, 01_00_00_00_00=256, 357959184 Hz 
    longmove(@timings,@timings256,16)
  if (mode==320)                              'NTSC 50 Hz signaling 1026x524, active 800x480, 100x30 text, NO PLACE FOR wide border, 2 colors per pixel, 01_01_00_00_00=320, 322162162 Hz 
    longmove(@timings,@timings320,16)
  if (mode==384)                              'NTSC 50 Hz signaling 912x524, active 800x480, 100x30 text, NO PLACE FOR wide border, 2 colors per pixel, 01_10_00_00_00=384, 286363636 Hz 
    longmove(@timings,@timings384,16)
  if (mode==448)                              'NTSC 50 Hz signaling 1026x524, active 880x496, 110x31 text, borderless, 2 colors per pixel, 01_11_00_00_00=496, 322162162 Hz 
    longmove(@timings,@timings448,16)

  repeat i from 0 to 5
    timings[i]:=timings[i]+streamer[i]+hdmibase<<17
  clkfreq:=timings[13]
  hubset(timings[14])
  
cpl:=timings[11]
lines:=timings[12]>>4
buflen:=cpl*lines
buf_ptr:=$7C000-4*buflen    

'--------- Start the driver with graphics mode 'mode' at pins 'base'

pub start(mode,base):result

' initialize pointers and variables

border_ptr:=@bordercolor
font_ptr:=@vga_font
hdmibase:=base

' the mode has to be set here to enable computing the buffer length

setmode(mode)



vblank_ptr:=@vblank
cursor_ptr:=@cursor_x
mode_ptr:=@timings
palette_ptr:=@ataripalette
command_ptr:=@cmd1
fontnum_ptr:=@fontnum

fontnum:=0  ' PC type font ' TODO: font# in buffer byte #1
bytemove(@colors,@vgacolors,16)

' initialize a cursor

cursor_x:=0
cursor_y:=0
cursor_sh:=14

cmd1:=257 ' set command idle

' start the cog

cog:=coginit(16,@hdmi, @buf_ptr)
waitms(20)

' clear the screen and set the colors to green on black

cls(c_green,c_black)
setbordercolors(0,255,0)
setwritecolors(c_green,c_black)

return cog

'**********************************************************************************
'
'        Fonts and palettes
'
'**********************************************************************************
dat
vga_font                file "vgafont.def"
st_font                 file "st4font.def"
ataripalette            file "ataripalettep2.def"

'**********************************************************************************
'
'        Timings and colors definitions
'
'**********************************************************************************

timingsxxt      long $70810000, $70810000, $70810000, $70810000, $70810000, $20880000 'streamer sets for text mode, to add to timings[0..5]

'                     bf.hs, hs,  bf.vis  visible, lr bord, pixel, ud bord,  up p., vsync, down p., mode, cpl, scanlines,  clock,                  hubset                  reserved
timings000      long   16,   80,    20,    1024,    112,     8,     48,      16,      16,    16,      0,  100,   480,     354693878,   %1_110000__11_0110_1100__1111_1011,   0
timings064      long   18,   64,    16,     928,     24,     8,     24,      24,      32,    24,     64,  110,   496,     319215686,   %1_110010__11_0010_1101__1111_1011,   0
timings128      long   16,   64,    16,     816,      8,     8,     8,       32,      64,    32,    128,  100,   480,     283750000,   %1_111111__11_1000_1011__1111_1011,   0
timings192      long   16,   80,    20,    1024,      0,     8,     0,       16,      16,    16,    192,  128,   576,     354693878,   %1_110000__11_0110_1100__1111_1011,   0
timings256      long   80,  160,    84,     816,      8,     8,     8,        8,      12,     8,    256,  100,   480,     357959184,   %1_110000__11_0110_0100__1111_1011,   0
timings320      long   80,   50,    80,     816,      8,     8,     8,        8,      12,     8,    320,  100,   480,     322162162,   %1_100100__10_0101_0011__1111_1011,   0
timings384      long   24,   48,    24,     816,      8,     8,     8,        8,      12,     8,    384,  100,   480,     286363636,   %1_010101__01_0011_1010__1111_1011,   0
timings448      long   40,   64,    42,     880,      0,     8,     0,        8,      12,     8,    448,  110,   480,     322162162,   %1_100100__10_0101_0011__1111_1011,   0

vgacolors       byte   0, 117, 199, 151, 39, 71, 246, 10, 5, 123, 203, 155, 43, 75, 234, 15


'**********************************************************************************
'
'        PASM driver code
'
'**********************************************************************************

DAT             org

hdmi            setq    #9
                rdlong  framebuf,  ptra++               'read pointers

                setq2   #255
                rdlong  $000, paletteptr                'read palette

                wrlong  #charcolor,#0
                setcmod #$100                           'enable HDMI mode
                mov     ii,#7
                shl     ii,#6
                add     ii,hbase
                drvl    ii                               ' #7<<6 + hdmi_base               'enable HDMI pins
                wrpin   ##%10110_1111_0111_10_00000_0, ii  '#7<<6 + hdmi_base      ' a '123 ohm BITDAC for pins

                setxfrq ##$0CCCCCCC+1                   'set streamer freq to 1/10th clk (25 MHz)


'' Blank lines before the frame

p101            setq    #12
                rdlong  m_bs,modeptr                     ' read timings
                testb   i_modenum,#9 wc                  ' bit #8 set = graphics mode
         if_c   jmp #p200								 ' jump to graphics mode       
p901            add     frames,#1
                  rdlong  fontstart,fontnumptr           ' todo: free byte in char buf = font select
                  add     fontstart,fontbuf
                rdlong  cursorx, cursorptr
                getbyte cursory, cursorx,#1
                getbyte cursorsh,cursorx,#2
                and     cursorx,#255

                mov     borderptr2,borderptr
                rdlong  border,borderptr2
                add     borderptr2,#4
                shl     border,#8

                rdlong  t1,commandptr
                add     commandptr,#4
                rdlong  t2,commandptr
                sub     commandptr,#4
                cmp     t1,#257  wcz
         if_lt  wrlut   t2,t1
                mov     t1, #257
                wrlong  t1,commandptr
                mov     hsync0,sync_000                 'vsync off
                mov     hsync1,sync_001
                callpa  i_upporch ,#blank
                wrbyte  #0,vblankptr

'' Upper border.

                testb   frames,#4 wz                    'cursor blinks at framerate/16, todo: define
         if_z   mov     cursorx,#129


                 mov     ii, i_vborder wz
    
                 if_z jmp #p111

up_border       call    #hsync
                xcont   m_vi,border
                djnz    ii,#up_border

'' 480 active lines

p111            mov     ii,i_lines
                mov     linenum,#0

line1           mov     cursorpos2,cursorx

                getnib  fontline,linenum,#0             'fontline is 0 to 15, a line in font def
                mov     framebuf2,framebuf              'compute a line start in the framebuffer, which is (line div 16)*100
                mov     linestart,linenum
                shr     linestart,#4
                cmp     linestart,cursory wz
          if_nz mov     cursorpos2,#129
                mul     linestart,i_cpl
                shl     linestart,#2
                add     framebuf2,linestart
                call    #hsync
                cmp     fontline,#0 wz
          if_nz jmp     #p102                          'if fontline >0, display it

                rdlong  border,borderptr2
                add     borderptr2,#4
                shl     border,#8

p102            getword t1,m_border,#0
                cmp     t1, #0 wz
          if_nz xcont   m_border,border                 'display a left border
                cmp     fontline,cursorsh wcz
          if_c  mov     cursorpos,#129
         if_nc  mov     cursorpos,cursorpos2
                add     cursorpos,#1
                mov     t2,fontstart
                add     t2,fontline
                rep     @p103,i_cpl                      'display cpl chars
''---------------------------------------------------------------------------------------
                        rdlong  char,framebuf2                                                      '1
                        getbyte backcolor,char,#2                                                   '2
                        getbyte charcolor,char,#3                                                   '3
                        getbyte char,char, #0                                                       '4
                        add     framebuf2,#4                                                        '5
                        shl     char,#4                                                             '6
                        add     char,t2                                                             '7
                        rdbyte  t1,char                                                             '8
                        sub     cursorpos,#1 wz                                                     '9
                if_z    xor     t1, #$FF                                                            '10

                        mergeb  t1																	'11
                        xcont   m_lut1,t1                                                           '12

                        rdlut   t1,backcolor                                                        '13
                        wrlut   t1,lutaddr                                                          '14
                        add     lutaddr,#1                                                          '15

                        rdlut   t1,charcolor                                                        '16
                        wrlut   t1,lutaddr                                                          '17
                        sub     lutaddr,#1                                                          '18

                        xor     m_lut1,a00010000                                                    '19
                        xor     lutaddr,#32                                                         '20
           
' 8 nops left      
    

'' rep end ----------------------------------------------------------------------------


p103            getword t1,m_border,#0
                cmp     t1, #0 wz
                if_nz xcont   m_border,border                     'display a right border

                add     linenum, #1
                djnz    ii,#line1

'' end of active screen
                wrlong  #1,vblankptr
                mov     ii,i_vborder wz                         'lower border
          if_z jmp #p112
                  
                 rdlong  border,borderptr2
                 add     borderptr2,#4
                 shl     border,#8
down_border      call    #hsync
                 xcont   m_vi,border
                 djnz    ii,#down_border
p112             callpa  i_downporch ,#blank                  'bottom blanks

                mov     hsync0,sync_222                      'vsync on
                mov     hsync1,sync_223
                callpa  i_vsync,#blank                       'vertical sync blanks
                jmp     #p101                                'loop
                
                
p200			


                rep     @p203,pixels                      'display 100 chars
''---------------------------------------------------------------------------------------
                        rdlong  char,framebuf2                                                      '2
                        getbyte backcolor,char,#2                                                   '4
                        getbyte charcolor,char,#3                                                   '6
                        getbyte char,char, #0                                                       '8
                        add     framebuf2,#4                                                        '10
                        shl     char,#4                                                             '12
                        add     char,t2                                                             '14
                        rdbyte  t1,char                                                             '16
                        sub     cursorpos,#1 wz                                                     '18
                if_z    xor     t1, #$FF                                                            '20


                        mergeb  t1
                        xcont   m_lut1,t1                                                           '22

                        rdlut   t1,backcolor                                                        '25
                        wrlut   t1,lutaddr                                                          '28
                        add     lutaddr,#1                                                          '30

                        rdlut   t1,charcolor                                                        '33
                        wrlut   t1,lutaddr                                                          '46
                        sub     lutaddr,#1                                                          '38

                        xor     m_lut1,a00010000                                                    '40
                        xor     lutaddr,#32                                                         '42    .....    38 left

'' rep end ----------------------------------------------------------------------------



p203                jmp    #p901             

'' Subroutines

blank           call    #hsync                          'blank lines
                xcont   m_vi,hsync0
        _ret_   djnz    pa,#blank

hsync           xcont   m_bs,hsync0                     'horizontal sync
                xzero   m_sn,hsync1
        _ret_   xcont   m_bv,hsync0

'' consts and vars

sync_000        long    %1101010100_1101010100_1101010100_10    '
sync_001        long    %1101010100_1101010100_0010101011_10    '        hsync
sync_222        long    %0101010100_0101010100_0101010100_10    'vsync
sync_223        long    %0101010100_0101010100_1010101011_10    'vsync + hsync

border          long    %00000000_00011010_00101100_00000000

m_bs            long    $70810000 + hdmi_base<<17 + 16          'before sync
m_sn            long    $70810000 + hdmi_base<<17 + 80          'sync
m_bv            long    $70810000 + hdmi_base<<17 + 20          'before visible
m_vi            long    $70810000 + hdmi_base<<17 + 1024        'visible 1024
m_border        long    $70810000 + hdmi_base<<17 + 112         'immediate 112
m_lut1          long    $20880000 + hdmi_base<<17 + 8          ' imm+lut, 1 char
i_vborder       long    48
i_upporch       long    16
i_vsync         long    16
i_downporch     long    16
i_modenum       long    0
i_cpl		    long    0
i_lines         long    0

linestart       long    0
linenum         long    0
a00010000       long    $00010000
lutaddr         long    256

cursorsh        long    14
frames          long    0
cursorx         long    0
cursory         long    0
cursorpos       long    0
cursorpos2      long    0
fontstart       long    0
 
spritex			long    0
spritey         long    0
pixels          long    1024
a576 long 576
framebuf        res     1
fontbuf         res     1
borderptr       res     1
vblankptr       res     1
cursorptr       res     1
modeptr         res     1
paletteptr      res     1
commandptr      res     1

fontnumptr      res     1
hbase           res     1
borderptr2      res     1

ii              res     1
framebuf2       res     1
hsync0          res     1
hsync1          res     1
fontline        res     1
t1              res     1
t2              res     1
char            res     1
backcolor       res     1
charcolor       res     1
repnum res 1

                fit     496                     '

end             long 0
{{
------------ MIT License ---------------
}}
