'********************************************************************************************
' Nostalgic HDMI for P2 - 1 cog version
' Version 0.09 alpha -24.03.2021
' 1 cog, 100x30 text with border, 8x16 font
' (c) 2012-2021 Piotr Kardasz pik33@o2.pl
' MIT license: see end of file
'********************************************************************************************

CON


hdmi_base       = 48             'must be a multiple of 8
_clkfreq        = 200000000      'start safe, hubset will do the rest


' 354_689_500   '200x Atari 8-bit PAL,  50x Amiga PAL  - real settings: 49 869 354693878  - 1.0000123 - mode 0
' 357_954_500   '200x Atari 8-bit NTSC, 50x Amiga NTSC - real settings: 49 877 357959184  - 1.0000131 - mode 256

' We also need x180 and x160 speeds
'
' 319_220_550 90x PAL  - real settings: 51 814 319215686 - 0.9999848 - mode 1
' 322_159_050 90x NTSC - real settings: 37 596 322162162 - 1.0000097 - mode 257

' 283_751_600 80x PAL  - real settings: 64 908 283750000 - 0.9999944 - mode 2
' 286_363_600 80x NTSC - real settings: 22 315 286363636 - 1.0000001 - mode 258


c_black         =    0
c_blue          =  117
c_green         =  199
c_cyan          =  151
c_red           =   39
c_magenta       =   71
c_brown         =  246
c_lightgray     =   10
c_darkgray      =    5
c_lightblue     =  123
c_lightgreen    =  203
c_lightcyan     =  155
c_lightred      =   43
c_lightmagenta  =   75
c_yellow        =  234
c_white         =   15


var

long buf_ptr
long font_ptr
long border_ptr
long vblank_ptr
long cursor_ptr
long mode_ptr
long palette_ptr
long command_ptr
long fontnum_ptr
long hdmibase
long cog
long bordercolor[32]

byte cursor_x
byte cursor_y
byte cursor_sh
byte dummy
long write_color
long write_background
byte colors[16]
long vblank
byte n_string[12]  ' string buffer for inttostr and hextostr
long timings[11]
long cmd1
long cmd2
long fontnum

{{
m_bs        - before sync,             16      80
m_sn        - sync                     80     160
m_bv        - before visible           20      84
m_vi        - visible                1024     816
m_border    - left and right borders  112       8
m_lut1      - pixels per char           8       8
i_vborder   - up and down border       48       8
i_upporch   - up non visible           16       8
i_vsync     - vsync                    16      12
i_downporch - down non visible         16       8
}}

pub dummy2()
'' this is not a main program.

pub setfontnum(afontnum)

fontnum:=afontnum

''-----------------------------------------------------------------------
pub defchar(fn,ch,b0,b1,b2,b3,b4,b5,b6,b7,b8,b9,b10,b11,b12,b13,b14,b15) :s

s:=@st_font+fn+ch*16
byte[s+00]:=b0
byte[s+01]:=b1
byte[s+02]:=b2
byte[s+03]:=b3
byte[s+04]:=b4
byte[s+05]:=b5
byte[s+06]:=b6
byte[s+07]:=b7
byte[s+08]:=b8
byte[s+09]:=b9
byte[s+10]:=b10
byte[s+11]:=b11
byte[s+12]:=b12
byte[s+13]:=b13
byte[s+14]:=b14
byte[s+15]:=b15


'*************************************************************************
'                                                                        *
'  Cursor functions                                                      *
'                                                                        *
'*************************************************************************

pub cursoron()

''---------- Switch the cursor on

cursor_sh+=16

pub cursoroff()

''---------- Switch the cursor on

cursor_sh-=16

pub setcursorpos(x,y)

''---------- Set the (x,y) position of cursor

cursor_x:=x
cursor_y:=y

pub setcursorshape(shape)

''---------- Define a cursor shape (0-full..15-line)

cursor_sh:=shape

'*************************************************************************
'                                                                        *
'  VBlank functions                                                      *
'                                                                        *
'*************************************************************************

pub waitvbl(amount) | i

''---------- Wait for start of vblank. Amount=delay in frames

repeat i from 1 to amount
  repeat until vblank==0
    waitus(100)
  repeat until vblank==1
    waitus(100)


pub waitvblend(amount) | i

''---------- Wait for end of vblank. Amount=delay in frames

repeat i from 1 to amount
  repeat until vblank==1
    waitus(100)
  repeat until vblank==0
    waitus(100)

'*************************************************************************
'                                                                        *
'  Color functions                                                       *
'                                                                        *
'*************************************************************************

pub setscreencolors(ff,bb) | c ,i

''---------- Set font and back colors for all screen - from 256 color palette

c:=ff<<24+bb<<16

repeat i from 0 to 2999
   long[buf_ptr+4*i]:= (long[buf_ptr+4*i] & $FFFF) |  c


pub setbordercolors(r,g,b) |color, i

''---------- Set border color for all screen - rgb

color:=r<<16+g<<8+b

repeat i from 0 to 31
  bordercolor[i]:=color



pub setfontcolor(x,y,c) |   place, color

''---------- Set the colors for the character at line (0..29) and position (0..99)

place:=(x)+(y*100)

if (place>2999)
  place:=2999

color:=(long[buf_ptr+4*place] &$FFFFFF) | (c<<24)
long[buf_ptr+4*place]:=color


pub setbackcolor(x,y,c) | place, color

''---------- Set the background color at line (0..29) and position (0..99)

place:=(x)+(y*100)

if (place>2999)
  place:=2999

color:=(long[buf_ptr+4*place] &$FF00FFFF) | (c<<16)
long[buf_ptr+4*place]:=color



pub setbordercolor(line,r,g,b) | color

''---------- Set the border color at line 0..31.
''---------- 0 is upper border, 31 is lower border, add #1 to the text line #

color:=r<<16+g<<8+b
bordercolor[line]:=color



pub setwritecolors(ff,bb)

''---------- Set colors for write and writeln

write_color:=ff
write_background:=bb

pub setcolor(c,r,g,b)  |cc

''---------- Set color #c in palette to r,g,b

cc:=r<<24+g<<16+b<<8
cmd2:=cc
cmd1:=c
repeat until cmd1==257


'*************************************************************************
'                                                                        *
'  Text functions                                                        *
'                                                                        *
'*************************************************************************

pub outtextxy(x,y,text) | iii

''--------- Output a string at position x,y - set the colors first

repeat iii from 0 to strsize(text)-1
  long[buf_ptr+4*(100*y+x+iii)]:=(long[buf_ptr+4*(100*y+x+iii)] & $FFFFFF00)  | byte[text+iii]

pub outtextxyc(x,y,text,f,b) | iii,c

''--------- Output a string at position x,y and colors b,f

c:=f<<24+b<<16
repeat iii from 0 to strsize(text)-1
  long[buf_ptr+4*(100*y+x+iii)]:=(long[buf_ptr+4*(100*y+x+iii)] & $FFFFFF00)  | byte[text+iii]
  long[buf_ptr+4*(100*y+x+iii)]:=(long[buf_ptr+4*(100*y+x+iii)] & $0000FFFF)  | c

pub putchar(achar) | c

''---------- Output a char at the cursor position, move the cursor

c:=write_color<<24+write_background<<16
long[buf_ptr+4*(100*cursor_y+cursor_x)]:=long[4*(buf_ptr+100*cursor_y+cursor_x)] &$FFFF | c
long[buf_ptr+4*(100*cursor_y+cursor_x)]:=(long[4*(buf_ptr+100*cursor_y+cursor_x)] & $FFFFFF00)  | (achar & $000000FF)
cursor_x+=1
if cursor_x==100
  cursor_x:=0
  cursor_y+=1
  if cursor_y>29
    scrollup()
    cursor_y:=29

pub putcharxy(x,y,achar) | c

''---------- Output a char at the cursor position, move the cursor

c:=write_color<<24+write_background<<16
long[buf_ptr+4*(100*y+x)]:=long[4*(buf_ptr+100*y+x)] &$FFFF | c
long[buf_ptr+4*(100*y+x)]:=(long[4*(buf_ptr+100*y+x)] & $FFFFFF00)  | (achar & $000000FF)

pub write(text) | iii,c,ncx,ncy

''--------- Output a string at the cursor position x,y, move the cursor

c:=write_color<<24+write_background<<16
'debug(uhex_long(write_color))
ncy:=cursor_y
ncx:=cursor_x+strsize(text)
repeat while ncx>99
  ncx-=100
  ncy+=1
repeat while ncy>29
  ncy-=1
  scrollup()
  cursor_y-=1

repeat iii from 0 to strsize(text)-1

  long[buf_ptr+4*(100*cursor_y+cursor_x+iii)]:=long[buf_ptr+4*(100*cursor_y+cursor_x+iii)] &$FFFF | c

outtextxy(cursor_x,cursor_y,text)

cursor_x:=ncx
cursor_y:=ncy

pub writeln(text)

'--------- Output a string at the cursor position x,y, move the cursor to the next line

write(text)
cursor_x:=0
cursor_y+=1
if (cursor_y>29)
  scrollup()
  cursor_y:=29

pub cls(fc,bc)   :c

''---------- Clear the screen, set its foreground/background color

c:=fc<<24+bc<<16+$20
longfill(buf_ptr,c,3000)
 setwritecolors(fc,bc)

pub scrollup() | i

''-----------  Scroll the screen one line up

longmove(buf_ptr,buf_ptr+400,2900)
repeat i from 2900 to 2999
  long[buf_ptr+4*i]:=(long[buf_ptr+4*i] & $FFFF0000) | 32

pub scrolldown() | i

''----------- Scroll the screen one line down

longmove(buf_ptr+400,buf_ptr,2900)
repeat i from 0 to 99
  long[buf_ptr+4*i]:=(long[buf_ptr+4*i] & $FFFF0000) | 32

pub crlf()

''----------- Set cursor at the first character in a new line, scroll if needed

cursor_x:=0
cursor_y+=1
if cursor_y>29
  scrollup()
  cursor_y:=29

''---------- Backspace

pub bksp()

cursor_x-=1
if cursor_x==255
  cursor_x:=99
  cursor_y-=1
  if cursor_y==255
    cursor_y:=0
    scrollup()

outtextxy(cursor_x,cursor_y,string(" "))


pub inttostr(i):result |q,pos,k,j

''---------- Convert a integer to dec string, return a pointer

j:=i
pos:=10
k:=0

if (j==0)
  n_string[0]:=48
  n_string[1]:=0

else
  if (j<0)
    j:=0-j
    k:=45

  n_string[11]:=0
  repeat while (pos>-1)
    q:=j//10
    q:=48+q
    n_string[pos]:=q
    j:=j/10
    pos-=1
  repeat while n_string[0]==48
    bytemove(@n_string,@n_string+1,12)

  if k==45
     bytemove(@n_string+1,@n_string,12)
     n_string[0]:=k

q:=@n_string
return q


pub inttohex(i,d):result |q,pos,k,j

''----------  Convert integer to hex string with d digits, return a pointer

j:=i
pos:=d-1
k:=0
n_string[d]:=0
repeat k from 0 to d-1
  n_string[k]:=48

if (j<>0)

  repeat while (pos>-1)
    q:=j//16
    if (q>9)
      q:=q+7
    q:=48+q
    n_string[pos]:=q
    j:=j/16
    pos-=1

q:=@n_string
return q


pub setmode(mode)

'' mode:nt_bb_cc_vv_hh
'' hh - h.zoom, vv-v.zoom, cc-c.depth, 
'' bb - borders/total pixels, 00-wide/1140, 01 medium/1026, 10 narrow/912, 11 no border, 1024 px PAL, 800 px NTSC

if (mode==0)

  timings[0]:= $70810000 + hdmi_base<<17 + 16          'before sync
  timings[1]:= $70810000 + hdmi_base<<17 + 80          'sync
  timings[2]:= $70810000 + hdmi_base<<17 + 20          'before visible
  timings[3]:= $70810000 + hdmi_base<<17 + 1024        'visible 1024
  timings[4]:= $70810000 + hdmi_base<<17 + 112         'immediate 112
  timings[5]:= $20880000 + hdmi_base<<17 + 8           'imm+lut, 1 char
  timings[6]:= 48
  timings[7]:= 16
  timings[8]:= 16
  timings[9]:= 16
  timings[10]:= 0

  clkfreq:=354693878
  org
  hubset ##%1_110000__11_0110_1100__1111_1011     '49,877->48,876->110000_11_0110_1100
  end


if (mode==64)   ' 1026 px per line

  timings[0]:= $70810000 + hdmi_base<<17 + 30          'before sync
  timings[1]:= $70810000 + hdmi_base<<17 + 80          'sync
  timings[2]:= $70810000 + hdmi_base<<17 + 20          'before visible
  timings[3]:= $70810000 + hdmi_base<<17 + 896         'visible 896
  timings[4]:= $70810000 + hdmi_base<<17 + 48          'immediate 48
  timings[5]:= $20880000 + hdmi_base<<17 + 8           'imm+lut, 1 char
  timings[6]:= 24
  timings[7]:= 32
  timings[8]:= 32
  timings[9]:= 32
  timings[10]:= 64

  clkfreq:=319215686
  org
  hubset ##%1_110010__11_0010_1101__1111_1011     '51,814->50,813->110010_11_0010_1101
  end


if (mode==128) ' 912 pixes per line     ' 283_751_600 80x PAL  - real settings: 64 908 283750000 - 0.9999944

  timings[0]:= $70810000 + hdmi_base<<17 + 16           'before sync
  timings[1]:= $70810000 + hdmi_base<<17 + 64           'sync
  timings[2]:= $70810000 + hdmi_base<<17 + 16           'before visible
  timings[3]:= $70810000 + hdmi_base<<17 + 816          'visible 1024
  timings[4]:= $70810000 + hdmi_base<<17 + 8            'immediate 112
  timings[5]:= $20880000 + hdmi_base<<17 + 8            'imm+lut, 1 char
  timings[6]:= 8
  timings[7]:= 32
  timings[8]:= 64
  timings[9]:= 32
  timings[10]:=128

  clkfreq:=283750000
  org
  hubset ##%1_111111__11_1000_1011__1111_1011     '64,908->63,907->110010_11_1000_1011
  end
  
if (mode==192) ' borderless 1024x576

  timings[0]:= $70810000 + hdmi_base<<17 + 16          'before sync
  timings[1]:= $70810000 + hdmi_base<<17 + 80          'sync
  timings[2]:= $70810000 + hdmi_base<<17 + 20          'before visible
  timings[3]:= $70810000 + hdmi_base<<17 + 1024        'visible 1024
  timings[4]:= 0                                      'no border
  timings[5]:= $20880000 + hdmi_base<<17 + 8           'imm+lut, 1 char
  timings[6]:= 0
  timings[7]:= 16
  timings[8]:= 16
  timings[9]:= 16
  timings[10]:= 192

  clkfreq:=354693878
  org
  hubset ##%1_110000__11_0110_1100__1111_1011     '49,877->48,876->110000_11_0110_1100
  end
  
  

if (mode==256)

  timings[0]:= $70810000 + hdmi_base<<17 + 80          'before sync
  timings[1]:= $70810000 + hdmi_base<<17 + 160         'sync
  timings[2]:= $70810000 + hdmi_base<<17 + 84          'before visible
  timings[3]:= $70810000 + hdmi_base<<17 + 816         'visible 816
  timings[4]:= $70810000 + hdmi_base<<17 + 8           'immediate 8
  timings[5]:= $20880000 + hdmi_base<<17 + 8           'imm+lut, 1 char
  timings[6]:= 8
  timings[7]:= 8
  timings[8]:= 12
  timings[9]:= 8
  timings[10]:= 256

  clkfreq:=357959184
  org
  hubset ##%1_110000__11_0110_0100__1111_1011     '49,869->48,868->110000_11_0110_0100
  end

if (mode==320)  '1026 px

  timings[0]:= $70810000 + hdmi_base<<17 + 80          'before sync
  timings[1]:= $70810000 + hdmi_base<<17 + 50          'sync
  timings[2]:= $70810000 + hdmi_base<<17 + 80          'before visible
  timings[3]:= $70810000 + hdmi_base<<17 + 816         'visible 816
  timings[4]:= $70810000 + hdmi_base<<17 + 8           'immediate 8
  timings[5]:= $20880000 + hdmi_base<<17 + 8           'imm+lut, 1 char
  timings[6]:= 8
  timings[7]:= 8
  timings[8]:= 12
  timings[9]:= 8
  timings[10]:= 320
  
  clkfreq:=322162162
  org
  hubset ##%1_100100__10_0101_0011__1111_1011     '37,596_.36,595->100100_10_0101_0011
  end

if (mode==384)  '912 px

  timings[0]:= $70810000 + hdmi_base<<17 + 24          'before sync
  timings[1]:= $70810000 + hdmi_base<<17 + 48         'sync
  timings[2]:= $70810000 + hdmi_base<<17 + 24          'before visible
  timings[3]:= $70810000 + hdmi_base<<17 + 816         'visible 816
  timings[4]:= $70810000 + hdmi_base<<17 + 8           'immediate 8
  timings[5]:= $20880000 + hdmi_base<<17 + 8           'imm+lut, 1 char
  timings[6]:= 8
  timings[7]:= 8
  timings[8]:= 12
  timings[9]:= 8
  timings[10]:= 384
   

  clkfreq:=286363636
  org
  hubset ##%1_010101__01_0011_1010__1111_1011     '22,315->21,314->010101_01_0011_1010
  end

if (mode==448)  '1026 px borderless 848x480

  timings[0]:= $70810000 + hdmi_base<<17 + 24          'before sync
  timings[1]:= $70810000 + hdmi_base<<17 + 48         'sync
  timings[2]:= $70810000 + hdmi_base<<17 + 24          'before visible
  timings[3]:= $70810000 + hdmi_base<<17 + 816         'visible 816
  timings[4]:= $70810000 + hdmi_base<<17 + 8           'immediate 8
  timings[5]:= $20880000 + hdmi_base<<17 + 8           'imm+lut, 1 char
  timings[6]:= 0
  timings[7]:= 10
  timings[8]:= 16
  timings[9]:= 10
  timings[10]:= 448
   

  clkfreq:=286363636
  org
  hubset ##%1_010101__01_0011_1010__1111_1011     '22,315->21,314->010101_01_0011_1010
  end

setbordercolors (0,0,0)


pub start(mode,base):result | c1

' mode=0 - start at PAL timings, 624 lines
' mode=256 - start at NTSC timings, 524 lines

'**********************************************************************************
'
'           Starts the driver
'
'**********************************************************************************

' initialize pointers

border_ptr:=@bordercolor
font_ptr:=@st_font
buf_ptr:=$7C000-3000    'todo: the buf ptr depends on mode
vblank_ptr:=@vblank
cursor_ptr:=@cursor_x
mode_ptr:=@timings
palette_ptr:=@ataripalette
command_ptr:=@cmd1
fontnum_ptr:=@fontnum

fontnum:=2048  ' ST type font
hdmibase:=base

' initialize a cursor

cursor_x:=0
cursor_y:=0
cursor_sh:=14

' set 16 VGA DOS like colors

colors[0] := c_black
colors[1] := c_blue
colors[2] := c_green
colors[3] := c_cyan
colors[4] := c_red
colors[5] := c_magenta
colors[6] := c_brown
colors[7] := c_lightgray
colors[8] := c_darkgray
colors[9] := c_lightblue
colors[$A]:= c_lightgreen
colors[$B]:= c_lightcyan
colors[$C]:= c_lightred
colors[$D]:= c_lightmagenta
colors[$E]:= c_yellow
colors[$F]:= c_white

' set timings
' We can have 1140,1026 or 912 pixes per line

setmode(mode)



  {{
m_bs        - before sync,             16      80
m_sn        - sync                     80     160
m_bv        - before visible           20      84
m_vi        - visible                1024     816
m_border    - left and right borders  112       8
m_lut1      - pixels per char           8       8
i_vborder   - up and down border       48       8
i_upporch   - up non visible           16       8
i_vsync     - vsync                    16      12
i_downporch - down non visible         16       8
    }}

cmd1:=257 ' set idle
cog:=coginit(16,@hdmi, @buf_ptr)
waitms(20)
cls(c_green,c_black)
setwritecolors(c_green,c_black)

return cog

{{ todo
pri poke(addr,val)


cmd2:=val
cmd1:=addr
repeat until (cmd1 == $FFFFFFFF)



pri peek(addr):cmd2a

cmd1:=addr+$FF000000
repeat until (cmd1 == $FFFFFFFF)
return cmd2a
}}


dat
st_font                 file "st4font.def"
vga_font                                file "vgafont.def"

dat

ataripalette            file "ataripalettep2.def"

'' One cog instead of 2 needed with P2 to do the job :)

DAT             org

hdmi            setq    #9
                rdlong  framebuf,  ptra++               'read pointers

                setq2   #255
                rdlong  $000, paletteptr                'read palette

                wrlong  #charcolor,#0
                setcmod #$100                           'enable HDMI mode
                mov     ii,#7
                shl     ii,#6
                add     ii,hbase
                 drvl    ii                               ' #7<<6 + hdmi_base               'enable HDMI pins
                 wrpin   ##%10110_1111_0111_10_00000_0, ii  '#7<<6 + hdmi_base      ' a '123 ohm BITDAC for pins
         '       drvl    #7<<6 + 48              'enable HDMI pins
          '      wrpin   ##%10110_1111_0111_10_00000_0, #7<<6 + 48     ' a '123 ohm BITDAC for pins
                setxfrq ##$0CCCCCCC+1                   'set streamer freq to 1/10th clk (25 MHz)


'' Blank lines before the frame

p101            setq    #10
                rdlong  m_bs,modeptr                    'read timings
                testb   i_modenum,#16 wc
         if_c   jmp #p200								' jump to graphics mode       
p901                add     frames,#1
                                rdlong  fontstart,fontnumptr
                                add     fontstart,fontbuf
                rdlong  cursorx, cursorptr
                getbyte cursory, cursorx,#1
                getbyte cursorsh,cursorx,#2
                and     cursorx,#255

                mov     borderptr2,borderptr
                rdlong  border,borderptr2
                add     borderptr2,#4
                shl     border,#8

                rdlong  t1,commandptr
                add     commandptr,#4
                rdlong  t2,commandptr
                sub     commandptr,#4
                cmp     t1,#257  wcz
         if_lt  wrlut   t2,t1
                mov     t1, #257
                wrlong  t1,commandptr
                mov     hsync0,sync_000                 'vsync off
                mov     hsync1,sync_001
                callpa  i_upporch ,#blank
                wrbyte  #0,vblankptr

'' Upper border.

                testb   frames,#4 wz                    'cursor blinks at framerate/16, todo: define
         if_z   mov     cursorx,#129


                 mov     ii, i_vborder wz
                 if_z mov ii,a576     
                 if_z jmp #p111

up_border       call    #hsync
                xcont   m_vi,border
                djnz    ii,#up_border

'' 480 active lines

                mov     ii,#480
p111            mov     linenum,#0

line1           mov     cursorpos2,cursorx

                getnib  fontline,linenum,#0             'fontline is 0 to 15, a line in font def
                mov     framebuf2,framebuf              'compute a line start in the framebuffer, which is (line div 16)*100
                mov     linestart,linenum
                shr     linestart,#4
                cmp     linestart,cursory wz
         if_nz  mov     cursorpos2,#129
                cmp m_border, #0 wz
        if_nz        mul     linestart,#400
        if_z         shl linestart,#9
                add     framebuf2,linestart
                call    #hsync
                cmp     fontline,#0 wz
         if_nz  jmp     #p102                          'if fontline >0, display it

                rdlong  border,borderptr2
                add     borderptr2,#4
                shl     border,#8

p102            cmp m_border, #0 wz
       if_nz    xcont   m_border,border                 'display a left border
       if_z     mov repnum,#128
       if_nz    mov repnum, #100
                cmp     fontline,cursorsh wcz
         if_c   mov     cursorpos,#129
        if_nc   mov     cursorpos,cursorpos2
                add     cursorpos,#1
                mov     t2,fontstart
                add     t2,fontline
                rep     @p103,repnum                      'display 100 chars
''---------------------------------------------------------------------------------------
                        rdlong  char,framebuf2                                                      '2
                        getbyte backcolor,char,#2                                                   '4
                        getbyte charcolor,char,#3                                                   '6
                        getbyte char,char, #0                                                       '8
                        add     framebuf2,#4                                                        '10
                        shl     char,#4                                                             '12
                        add     char,t2                                                             '14
                        rdbyte  t1,char                                                             '16
                        sub     cursorpos,#1 wz                                                     '18
                if_z    xor     t1, #$FF                                                            '20


                        mergeb  t1
                        xcont   m_lut1,t1                                                           '22

                        rdlut   t1,backcolor                                                        '25
                        wrlut   t1,lutaddr                                                          '28
                        add     lutaddr,#1                                                          '30

                        rdlut   t1,charcolor                                                        '33
                        wrlut   t1,lutaddr                                                          '46
                        sub     lutaddr,#1                                                          '38

                        xor     m_lut1,a00010000                                                    '40
                        xor     lutaddr,#32                                                         '42    .....    38 left

'' rep end ----------------------------------------------------------------------------


p103            cmp m_border,#0 wz
                if_nz xcont   m_border,border                     'display a right border

                add     linenum, #1
                djnz    ii,#line1

'' end of active screen
                wrlong  #1,vblankptr
                mov     ii,i_vborder wz                         'lower border
          if_z jmp #p112
                  
                 rdlong  border,borderptr2
                 add     borderptr2,#4
                 shl     border,#8
down_border      call    #hsync
                 xcont   m_vi,border
                 djnz    ii,#down_border
p112             callpa  i_downporch ,#blank                  'bottom blanks

                mov     hsync0,sync_222                      'vsync on
                mov     hsync1,sync_223
                callpa  i_vsync,#blank                       'vertical sync blanks
                jmp     #p101                                'loop
                
                
p200			


                rep     @p203,pixels                      'display 100 chars
''---------------------------------------------------------------------------------------
                        rdlong  char,framebuf2                                                      '2
                        getbyte backcolor,char,#2                                                   '4
                        getbyte charcolor,char,#3                                                   '6
                        getbyte char,char, #0                                                       '8
                        add     framebuf2,#4                                                        '10
                        shl     char,#4                                                             '12
                        add     char,t2                                                             '14
                        rdbyte  t1,char                                                             '16
                        sub     cursorpos,#1 wz                                                     '18
                if_z    xor     t1, #$FF                                                            '20


                        mergeb  t1
                        xcont   m_lut1,t1                                                           '22

                        rdlut   t1,backcolor                                                        '25
                        wrlut   t1,lutaddr                                                          '28
                        add     lutaddr,#1                                                          '30

                        rdlut   t1,charcolor                                                        '33
                        wrlut   t1,lutaddr                                                          '46
                        sub     lutaddr,#1                                                          '38

                        xor     m_lut1,a00010000                                                    '40
                        xor     lutaddr,#32                                                         '42    .....    38 left

'' rep end ----------------------------------------------------------------------------



p203                jmp    #p901             

'' Subroutines

blank           call    #hsync                          'blank lines
                xcont   m_vi,hsync0
        _ret_   djnz    pa,#blank

hsync           xcont   m_bs,hsync0                     'horizontal sync
                xzero   m_sn,hsync1
        _ret_   xcont   m_bv,hsync0

'' consts and vars

sync_000        long    %1101010100_1101010100_1101010100_10    '
sync_001        long    %1101010100_1101010100_0010101011_10    '        hsync
sync_222        long    %0101010100_0101010100_0101010100_10    'vsync
sync_223        long    %0101010100_0101010100_1010101011_10    'vsync + hsync

border          long    %00000000_00011010_00101100_00000000

m_bs            long    $70810000 + hdmi_base<<17 + 16          'before sync
m_sn            long    $70810000 + hdmi_base<<17 + 80          'sync
m_bv            long    $70810000 + hdmi_base<<17 + 20          'before visible
m_vi            long    $70810000 + hdmi_base<<17 + 1024        'visible 1024
m_border        long    $70810000 + hdmi_base<<17 + 112         'immediate 112
m_lut1          long    $20880000 + hdmi_base<<17 + 8          ' imm+lut, 1 char
i_vborder       long    48
i_upporch       long    16
i_vsync         long    16
i_downporch     long    16
i_modenum       long    0

linestart       long    0
linenum         long    0
a00010000       long    $00010000
lutaddr         long    256

cursorsh        long    14
frames          long    0
cursorx         long    0
cursory         long    0
cursorpos       long    0
cursorpos2      long    0
fontstart       long    0
 
spritex			long    0
spritey         long    0
pixels          long    1024
a576 long 576
framebuf        res     1
fontbuf         res     1
borderptr       res     1
vblankptr       res     1
cursorptr       res     1
modeptr         res     1
paletteptr      res     1
commandptr      res     1

fontnumptr      res     1
hbase           res     1
borderptr2      res     1

ii              res     1
framebuf2       res     1
hsync0          res     1
hsync1          res     1
fontline        res     1
t1              res     1
t2              res     1
char            res     1
backcolor       res     1
charcolor       res     1
repnum res 1

                fit     496                     '

dat
end             long 0
{{
------------ MIT License ---------------
}}
